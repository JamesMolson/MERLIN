! MERLIN Example FODO Damping Ring Lattice
! Version 1.0: A. Wolski (LBNL) Oct 21, 2004

  ASSIGN, PRINT="MERLINFodo.print.txt"
  SETPLOT, LWIDTH=3, ASCALE=1.0, LSCALE=1.0, RSCALE=1.0, &
           XSIZE=21, YSIZE=15

! ==============================================================================
! Load XSIF file
! ------------------------------------------------------------------------------

  CALL, "MERLINFodo.XSIF"

! ==============================================================================
! SUBROUTINEs
! ------------------------------------------------------------------------------

  MATCHARC : SUBROUTINE

  USE, ARCCELL
  CELL

    VARY, ARCQ1K1, STEP=0.001, LOWER=-2.0, UPPER=2.0
    VARY, ARCQ2K1, STEP=0.001, LOWER=-2.0, UPPER=2.0

    CONSTRAINT, #E, MUX=0.265, MUY=0.245

    SIMPLEX, CALLS=5000, TOL=1.0E-9

  ENDMATCH

  ENDSUBROUTINE


! ==============================================================================
! COMMANDs
! ------------------------------------------------------------------------------

  BEAM, PARTICLE=electron, ENERGY=E0, NPART=2.0E10
  RF_ON := 0

! Match the FODO Cell and plot lattice functions -------------------------------

  MATCHARC

  USE,  ARCCELL
  PRINT, #E
  TWISS, SAVE

  PLOT, TABLE=TWISS, HAXIS=S, VAXIS1=BETX, BETY,  &
                              VAXIS2=DX,          &
        SPLINE, COLOUR=100, STYLE=1, RANGE=#S/#E, &
        TITLE="FODO Cell"


! Output final values of drift lengths and magnet strengths --------------------

  ASSIGN, ECHO="MERLINFodo.parameters.txt"

    VALUE, ARCQ1K1
    VALUE, ARCQ2K1

  USE,  ARCCELL
  RF_ON := 0

! Tune sextupoles for zero chromaticity ----------------------------------------

  ASSIGN, PRINT="MERLINFodo.harmon.txt"

  HARMON
    HVARY, ARCS1K2, STEP=0.001
    HVARY, ARCS2K2, STEP=0.001
    HTUNE
    HFUNCTIONS
  ENDHARM

  VALUE, ARCS1K2
  VALUE, ARCS2K2

! Run analysis routines --------------------------------------------------------

  ASSIGN, PRINT="MERLINFodo.bmpm.txt"

  USE, ARCCELL
  RF_ON  := 1
  SXT_ON := 1
  PRINT, #E
  SAVEBETA, LABEL=TWSSi, PLACE=#S
  TWISS, SAVE
  BMPM

  BEAM, PARTICLE=electron, ENERGY=E0, NPART=2.0E10, RADIATE
  EMIT


! Output lattice file for MERLIN input ----------------------------------------

  SELECT, OPTICS, CLEAR
  SELECT, OPTICS, #S/#E
  OPTICS, COLUMNS= NAME, KEYWORD, S, L, K0L, E1, E2, &
          K1L, K2L, K3L, TILT, TYPE, &
          FILENAME= "MERLINFodo.lattice.txt" 

STOP
! Tracking ----------------------------------------------------------------

  ASSIGN, PRINT="MERLINFodo.tracking.txt"

  BEAM, PARTICLE=electron, ENERGY=E0, NPART=2.0E10, RADIATE=.F.
  USE, ARCCELL
  RF_ON := 0

  EMITXNi:= 0.01
  EMITYNi:= 0.01

  EMITXi := EMITXNi/(E0/EMASS)
  EMITYi := EMITYNi/(E0/EMASS)
  GAMXi  := (1+TWSSi[ALFX]*TWSSi[ALFX])/TWSSi[BETX]
  GAMYi  := (1+TWSSi[ALFY]*TWSSi[ALFY])/TWSSi[BETY]
  SIGXi  := SQRT(EMITXi*TWSSi[BETX])
  SIGPXi := SQRT(EMITXi*GAMXi)
  ANGXi  := -TWSSi[ALFX]*SIGXi/TWSSi[BETX]
  SIGYi  := SQRT(EMITYi*TWSSi[BETY])
  SIGPYi := SQRT(EMITYi*GAMYi)
  ANGYi  := -TWSSi[ALFY]*SIGYi/TWSSi[BETY]

  TRACK

    SET, XOFF, SIGXi
    SET, XANG, ANGXi

    DO, TIMES=20
        START,X=XOFF,PX=XANG,Y=0,PY=0,T=0,DELTAP=0
        SET, XOFF, XOFF+SIGXi
        SET, XANG, XANG+ANGXi
    ENDDO

    RUN, METHOD=LIE4, TABLE=TRACKTABLE, TURNS=250

    PLOT, TABLE=TRACKTABLE, HAXIS=X, VAXIS=PX, &
          HMIN=-20*SIGXi,  HMAX=20*SIGXi,      &
          VMIN=-20*SIGPXi, VMAX=20*SIGPXi,     &
          SYMBOL=3, MULTIPLE,                  &
          TITLE="Horizontal phase space"

  ENDTRACK
  ARCHIVE, TABLE=TRACKTABLE, FILENAME="TrackXInj.txt"

  TRACK

    SET, YOFF, SIGYi/2
    SET, YANG, ANGYi/2

    DO, TIMES=20
        START,X=0,PX=0,Y=YOFF,PY=YANG,T=0,DELTAP=0
        SET, YOFF, YOFF+SIGYi
        SET, YANG, YANG+ANGYi
    ENDDO

    RUN, METHOD=LIE4, TABLE=TRACKTABLE, TURNS=250

    PLOT, TABLE=TRACKTABLE, HAXIS=Y, VAXIS=PY, &
          HMIN=-20*SIGYi,  HMAX=20*SIGYi,      &
          VMIN=-20*SIGPYi, VMAX=20*SIGPYi,     &
          SYMBOL=3, MULTIPLE,                  &
          TITLE="Vertical phase space"

  ENDTRACK
  ARCHIVE, TABLE=TRACKTABLE, FILENAME="TrackYInj.txt"

  
STOP
